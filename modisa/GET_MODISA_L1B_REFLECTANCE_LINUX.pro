;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_MODISA_L1B_REFLECTANCE_LINUX       
;* 
;* PURPOSE:
;*      RETURNS THE L1B REFLECTANCE FOR A SPECIFIC MODISA BAND
;* 
;* CALLING SEQUENCE:
;*      RES = GET_MODISA_L1B_REFLECTANCE_LINUX(FILENAME)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR REFLECTANCE EXTRACTION 
;*
;* KEYWORDS:
;*      VERBOSE      - PROCESSING STATUS OUTPUTS
;*      RES_1KM      - INDICATES THAT THE IN_BAND INDEX REFERS TO THE 1KM DATASET
;*      RES_500M     - INDICATES THAT THE IN_BAND INDEX REFERS TO THE 500M DATASET
;*      RES_250M     - INDICATES THAT THE IN_BAND INDEX REFERS TO THE 250M DATASET
;*
;* OUTPUTS:
;*      TOA_REFL     - TOA REFLECTANCE FOR PRODUCT FOLLOWING USE OF SCALING FACTOR 
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*                    M BOUVET - PROTOTYPE DIMITRI VERSION
;*      23 MAR 2011 - C KENT   - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      12 APR 2010 - C KENT    - WINDOWS 32-BIT MACHINE IDL 7.1: COMPILATION SUCCESSFUL, 
;*                                RESULTS EQUAL TO MANUAL EXTRACTION OF DATA USING HDF EXPLORER 
;*      12 APR 2011 - C KENT    - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL, 
;*                                NO APPARENT DIFFERENCES WHEN COMPARED TO WINDOWS MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_MODISA_L1B_REFLECTANCE_LINUX,FILENAME,VERBOSE=VERBOSE,RES_1KM=RES_1KM,RES_500M=RES_500M,RES_250M=RES_250M

;------------------------------------------------
; CHECK KEYWORDS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: STARTING REFLECTANCE RETRIEVAL'
  TEMP = N_ELEMENTS(RES_1KM)+N_ELEMENTS(RES_500M)+N_ELEMENTS(RES_250M)
  IF TEMP EQ 0 THEN BEGIN
    PRINT, 'MODISA L1B REFLECTANCE: NO RESOLUTION SPECIFIED, USING DEFAULT OF 1KM'
    RES_1KM = [1]
  ENDIF
  IF TEMP GT 1 THEN BEGIN
    PRINT, 'MODISA L1B REFLECTANCE: ERROR, MULTIPLE RESOLUTIONS SELECTED'
    RETURN,-1
  ENDIF

;------------------------------------------------
; CHECK FILENAME AND IN_BAND ARE NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'MODISA L1B REFLECTANCE: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF
 
;------------------------------------------------ 
; START THE SD INTERFACE AND OPEN THE PRODUCT 

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: STARTING IDL HDF SD INTERFACE'  
  HDF_ID = HDF_SD_START(FILENAME,/READ) 

;------------------------------------------------
; RETRIEVE THE 1KM WAVEBANDS

  IF N_ELEMENTS(RES_1KM) GT 0 THEN BEGIN

    IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: RETRIEVING 1KM'
    SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'EV_1KM_RefSB')
    SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
    HDF_SD_GETDATA,SDS_ID,TOA_REFL
    ;TOA_REFL_COUNTS = TOA_REFL_COUNTS[*,*,IN_BAND]
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_scales')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_SLOPES
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_offsets')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_OFFSET  

    HDF_SD_ENDACCESS, SDS_ID

    TOA_REFL = FLOAT(TEMPORARY(TOA_REFL))
    ;TOA_REFL_COUNTS = 0
    ;TEMP_DIMS = SIZE(TOA_REFL)
    ;TOA_REFL = TOA_REFL_SLOPES*(TOA_REFL-TOA_REFL_OFFSET)
  
  ENDIF
  
;------------------------------------------------
; RETRIEVE THE 500M WAVEBANDS

  IF N_ELEMENTS(RES_500M) GT 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: RETRIEVING 500M'
    SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'EV_500_Aggr1km_RefSB')
    SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
    HDF_SD_GETDATA,SDS_ID,TOA_REFL
    ;TOA_REFL_COUNTS = TOA_REFL_COUNTS[*,*,IN_BAND]
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_scales')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_SLOPES
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_offsets')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_OFFSET  

    HDF_SD_ENDACCESS, SDS_ID

    TOA_REFL = FLOAT(TEMPORARY(TOA_REFL))
    ;TOA_REFL_COUNTS = 0
    ;TEMP_DIMS = SIZE(TOA_REFL)
    ;TOA_REFL = TOA_REFL_SLOPES*(TOA_REFL-TOA_REFL_OFFSET)
  
  ENDIF 
  
;------------------------------------------------
; RETRIEVE THE 250M WAVEBANDS

  IF N_ELEMENTS(RES_250M) GT 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: RETRIEVING 250M'
    SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'EV_250_Aggr1km_RefSB')
    SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
    HDF_SD_GETDATA,SDS_ID,TOA_REFL
    ;TOA_REFL_COUNTS = TOA_REFL_COUNTS[*,*,IN_BAND]
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_scales')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_SLOPES
  
    ATTR_INDX = HDF_SD_ATTRFIND(SDS_ID, 'reflectance_offsets')
      IF ATTR_INDX GE 0 THEN HDF_SD_ATTRINFO, SDS_ID, ATTR_INDX, DATA=TOA_REFL_OFFSET  

    HDF_SD_ENDACCESS, SDS_ID

    TOA_REFL = FLOAT(TEMPORARY(TOA_REFL))
    ;TOA_REFL_COUNTS = 0
    ;TEMP_DIMS = SIZE(TOA_REFL)
    ;TOA_REFL = TOA_REFL_SLOPES*(TOA_REFL-TOA_REFL_OFFSET)
  
  ENDIF
  
;-----------------------------------------------
; CONVERT TO REFLECTANCE

  TEMP_DIMS = SIZE(TOA_REFL)
  FOR I=0,TEMP_DIMS[3]-1 DO BEGIN
  TOA_REFL[*,*,I] = TOA_REFL_SLOPES[I]*(TOA_REFL[*,*,I]-TOA_REFL_OFFSET[I])
  ENDFOR
    
;-----------------------------------------------
; CLOSE THE PRODUCT AND SD INTERFACE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'MODISA L1B REFLECTANCE: CLOSING PRODUCT AND RETURNING REFLECTANCE DATA'
  HDF_SD_END,HDF_ID  
  
;-----------------------------------------------
; RETURN,L1B_RAD ;NOTE, THIS IS RHO*SZA
  RETURN,TOA_REFL
 
END
