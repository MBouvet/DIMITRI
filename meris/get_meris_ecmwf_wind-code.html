<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.5.1 on Thu Jul  4 16:01:42 2013 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>get_meris_ecmwf_wind.pro (Documentation for /home/marrabld/projects/DIMITRI_2.0/Source/)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="get_meris_ecmwf_wind.pro (Documentation for /home/marrabld/projects/DIMITRI_2.0/Source/)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;**************************************************************************************</span>
<span class="comments">;**************************************************************************************</span>
<span class="comments">;*</span>
<span class="comments">;* NAME:</span>
<span class="comments">;*      GET_MERIS_ECMWF_WIND       </span>
<span class="comments">;* </span>
<span class="comments">;* PURPOSE:</span>
<span class="comments">;*      RETURNS THE ECMWF WIND VECTORS OF A MERIS IMAGE</span>
<span class="comments">;* </span>
<span class="comments">;* CALLING SEQUENCE:</span>
<span class="comments">;*      RES = GET_MERIS_ECMWF_WIND(FILENAME)      </span>
<span class="comments">;* </span>
<span class="comments">;* INPUTS:</span>
<span class="comments">;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR EXTRACTION      </span>
<span class="comments">;*</span>
<span class="comments">;* KEYWORDS:</span>
<span class="comments">;*     ENDIAN_SIZE  - MACHINE ENDIAN SIZE (0: LITTLE, 1: BIG)</span>
<span class="comments">;*     VERBOSE      - PROCESSING STATUS OUTPUTS</span>
<span class="comments">;*</span>
<span class="comments">;* OUTPUTS:</span>
<span class="comments">;*     STRUCT.ZONAL   - ECMWF WIND IN THE X-AXIS (EAST IS POSITIVE)</span>
<span class="comments">;*     STRUCT.MERID   - ECMWF WIND IN THE Y-AXIS (NORTH IS POSITIVE)</span>
<span class="comments">;*</span>
<span class="comments">;* COMMON BLOCKS:</span>
<span class="comments">;*     NONE </span>
<span class="comments">;*</span>
<span class="comments">;* MODIFICATION HISTORY:</span>
<span class="comments">;*     03 JUL 2011 - C KENT   - DIMITRI-2 V1.0</span>
<span class="comments">;*</span>
<span class="comments">;* VALIDATION HISTORY:</span>
<span class="comments">;*     01 DEC 2010 - C KENT   - </span>
<span class="comments">;*</span>
<span class="comments">;**************************************************************************************</span>
<span class="comments">;**************************************************************************************</span>

FUNCTION GET_MERIS_ECMWF_WIND,FILENAME,ENDIAN_SIZE=ENDIAN_SIZE,VERBOSE=VERBOSE

<span class="comments">;------------------------------------------------</span>
<span class="comments">; CHECK FILENAME IS NOMINAL</span>

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'MERIS L1B ECMWF WIND: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF

<span class="comments">;------------------------------------------------</span>
<span class="comments">; IF ENDIAN SIZE NOT PROVIDED THEN GET VALUE</span>

  IF N_ELEMENTS(ENDIAN_SIZE) EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN BEGIN
      PRINT, 'MERIS L1B ECMWF WIND: NO ENDIAN SIZE PROVIDED, RETRIEVING...'
      ENDIAN_SIZE = GET_ENDIAN_SIZE(/VERBOSE)
    ENDIF ELSE ENDIAN_SIZE = GET_ENDIAN_SIZE()
  ENDIF

<span class="comments">;------------------------------------------------</span>
<span class="comments">;DEFINE HEADER VARIABLES</span>

  MPH_SIZE = 1247
  SPH_SIZE = 9942
  FILE_MPH = BYTARR(MPH_SIZE)
  FILE_SPH = BYTARR(SPH_SIZE)

<span class="comments">;-----------------------------------------------</span>
<span class="comments">; OPEN THE FILE AND EXTRACT HEADER</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: OPENING PRODUCT'
  OPENR,PRD_VIG,FILENAME,/GET_LUN
  READU,PRD_VIG,FILE_MPH
  READU,PRD_VIG,FILE_SPH

<span class="comments">;-----------------------------------------------</span>
<span class="comments">; RETRIEVE: POSITION OF DSD, TP SUBSAMPLING FREQUENCY,LINE SIZE,TP PER LINE,DSD,OFFSET,DS_SIZE,NUMBER OF RECORDS, RECORD SIZE</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: RETRIEVING DSD INFORMATION'
  TP_DSD_POS      = STRPOS(FILE_SPH,'DS_NAME="Tie points ADS              "')
  TP_SFREQ        = STRMID(FILE_SPH, STRPOS(FILE_SPH,'SAMPLES_PER_TIE_PT=+')+20, 3)+0L
  LINE_SIZE       = STRMID(FILE_SPH, STRPOS(FILE_SPH,'LINE_LENGTH=+')+13, 5)+0L
  NB_TP_LINE      = FLOOR(LINE_SIZE/TP_SFREQ)+1
  
  TP_DSD          = STRMID(FILE_SPH, TP_DSD_POS,280)
  TP_OFFSET       = STRMID(TP_DSD, STRPOS(TP_DSD, 'DS_OFFSET=+')+11,20)+0L
  TP_SIZE         = STRMID(TP_DSD, STRPOS(TP_DSD, 'DS_SIZE=+')  +9,20)+0L
  TP_DSR_NUMBER   = STRMID(TP_DSD, STRPOS(TP_DSD, 'NUM_DSR=+')  +9,20)+0L
  TP_DSR_SIZE     = STRMID(TP_DSD, STRPOS(TP_DSD, 'DSR_SIZE=+') +10,20)+0L

<span class="comments">;-----------------------------------------</span>
<span class="comments">; DEFINE ARRAY AND TEMPORARY TO HOLD DATA</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: DEFINING DATA ARRAYS FOR OUTPUT'
  ECMWF_WIND_ZON = INTARR(NB_TP_LINE,TP_DSR_NUMBER)
  ECMWF_WIND_MER = INTARR(NB_TP_LINE,TP_DSR_NUMBER)
  VIEW_REC = INTARR(NB_TP_LINE)
  NODATA = BYTARR(2853)
   
<span class="comments">;-----------------------------------------</span>
<span class="comments">; LOOP OVER EACH RECORD AND EXTRACT ALL ECMWFDATA  </span>
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: STARTING LOOP FOR DATA EXTRACTION'
  FOR VREC=0,TP_DSR_NUMBER-1 DO BEGIN
 
    POINT_LUN,PRD_VIG, TP_OFFSET+VREC*TP_DSR_SIZE
    
    READU,PRD_VIG,NODATA

    READU,PRD_VIG,VIEW_REC
    ECMWF_WIND_ZON[*,VREC] = VIEW_REC

    READU,PRD_VIG,VIEW_REC
    ECMWF_WIND_MER[*,VREC] = VIEW_REC
    
  ENDFOR <span class="comments">;END OF LOOP ON ECMWF RECORDS</span>

<span class="comments">;----------------------------------------</span>
<span class="comments">; SWAP ENDIAN IF NEEDED - MERIS DATA IS BIG ENDIAN</span>
  
    IF ENDIAN_SIZE EQ 0 THEN BEGIN
      ECMWF_WIND_ZON   = SWAP_ENDIAN(ECMWF_WIND_ZON)
      ECMWF_WIND_MER   = SWAP_ENDIAN(ECMWF_WIND_MER)
    ENDIF
  
<span class="comments">;---------------------------------------</span>
<span class="comments">; CLOSE THE FILE</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: CLOSING PRODUCTS AND RELEASING THE LUN'
  CLOSE, PRD_VIG
  FREE_LUN, PRD_VIG
  
<span class="comments">;---------------------------------------</span>
<span class="comments">; FIND NUMBER OF PIXELS WITHIN MERIS RADIANCE FRAME</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: RETRIEVE DIMENSIONS OF RADIANCE PRODUCT'
  RD_POS      = STRPOS(FILE_SPH,'DS_NAME="Radiance MDS(1)             "')
  RD_DSD      = STRMID(FILE_SPH, RD_POS,280)
  NB_RD_LINE  = STRMID(RD_DSD, STRPOS(RD_DSD, 'NUM_DSR=+')+9,20)  

<span class="comments">;---------------------------------------</span>
<span class="comments">; CONVERT VALUES INTO CORRECT UNITS</span>
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: CONVERT VALUES INTO DEGREES'
  ECMWF_WIND_ZON   = FLOAT(ECMWF_WIND_ZON)*0.1
  ECMWF_WIND_MER   = FLOAT(ECMWF_WIND_MER)*0.1
  
<span class="comments">;---------------------------------------</span>
<span class="comments">; INTERPOLATE VALUES TO RADIANCE GRID</span>

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: REGRID DATA INTO RADIANCE PRODUCT DIMENSIONS'
  ECMWF_WIND_ZON   = CONGRID(ECMWF_WIND_ZON,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  ECMWF_WIND_MER   = CONGRID(ECMWF_WIND_MER,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B ECMWF WIND: RETURNING ECMWF WIND'
  RETURN,{ZONAL:ECMWF_WIND_ZON,MERID:ECMWF_WIND_MER}
  
END
</code>
    </div>
  </body>
</html>