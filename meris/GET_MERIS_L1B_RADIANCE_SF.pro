;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_MERIS_L1B_RADIANCE_SF       
;* 
;* PURPOSE:
;*      RETURNS THE L1B RADIANCE SCALING FACTOR FOR A SPECIFIC MERIS BAND
;* 
;* CALLING SEQUENCE:
;*      RES = GET_MERIS_L1B_RADIANCE_SF(FILENAME,IN_BAND)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR RADIANCE EXTRACTION 
;*      IN_BAND  - THE INDEX OF RADIANCE BAND TO BE RETURNED, STARTS FROM 0   
;*
;* KEYWORDS:
;*      ENDIAN_SIZE - MACHINE ENDIAN SIZE (0: LITTLE, 1: BIG)
;*      VERBOSE     - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      SCL_FAC     - SCALING FACTOR FOR IN_BAND  
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      17 NOV 2005 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      17 NOV 2010 - C KENT   - DIMITRI-2 V1.0
;*      20 NOV 2010 - C KENT   - UPDATED TO ALLOW SINGULAR USAGE (REMOVED COMMON BLOCKS)
;*      22 NOV 2010 - C KENT   - ADDED VERBOSE KEYWORD OPTION
;*      01 DEC 2010 - C KENT   - ADDED FILENAME AND IN_BAND ERROR CATCHING
;*      02 DEC 2010 - C KENT   - UPDATED HEADER INFORMATION
;*
;* VALIDATION HISTORY:
;*      01 DEC 2010 - C KENT    - WINDOWS 32-BIT MACHINE: COMPILATION SUCCESSFUL,
;*                                BAND RADIANCE EQUAL TO BEAM VISAT FOR MERIS L1B DATA
;*      05 JAN 2010 - C KENT    - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                                NO DIFFERENCES OBSERVED AGAINST WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_MERIS_L1B_RADIANCE_SF,FILENAME,IN_BAND,ENDIAN_SIZE=ENDIAN_SIZE,VERBOSE=VERBOSE

;------------------------------------------------
; CHECK FILENAME AND IN_BAND ARE NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF
 
  IF N_ELEMENTS(IN_BAND) NE 1 OR $
      IN_BAND[0] LT 0 OR $
      IN_BAND[0] GT 14 THEN BEGIN
    PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: ERROR, INPUT IN_BAND INCORRECT'
    RETURN,-1
  ENDIF 

;------------------------------------------------
; IF ENDIAN SIZE NOT PROVIDED THEN GET VALUE

  IF N_ELEMENTS(ENDIAN_SIZE) EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: NO ENDIAN SIZE PROVIDED, RETRIEVING...'
    ENDIAN_SIZE = GET_ENDIAN_SIZE(/VERBOSE)
    ENDIF ELSE ENDIAN_SIZE = GET_ENDIAN_SIZE()
  ENDIF

;------------------------------------------------
; DEFINE THE NUMBER OF MERIS BANDS

  NB_BANDS = 15 

;------------------------------------------------
;DEFINE HEADER VARIABLES

  MPH_SIZE = 1247
  SPH_SIZE = 9942
  FILE_MPH = BYTARR(MPH_SIZE)
  FILE_SPH = BYTARR(SPH_SIZE)

;-----------------------------------------------
; OPEN THE FILE AND EXTRACT HEADER

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: OPENING PRODUCT'
  OPENR,PRD_SF,FILENAME,/GET_LUN
  READU,PRD_SF,FILE_MPH
  READU,PRD_SF,FILE_SPH
  
;-----------------------------------------------
; CONVERT IN_BAND NUMBER TO STRING

  IN_BAND_STR = STRTRIM(STRING(IN_BAND+1),2)

;-----------------------------------------------
; RETRIEVE: POSITION OF DSD, DSD,OFFSET,DS_SIZE,NUMBER OF RECORDS, RECORD SIZE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FCATOR: RETRIEVING DSD INFORMATION'
  SCF_DSD_POS     = STRPOS(FILE_SPH,'"Scaling Factor GADS         "')
  SCF_DSD         = STRMID(FILE_SPH, SCF_DSD_POS,280)
  SCF_OFFSET      = STRMID(SCF_DSD, strpos(SCF_DSD, 'DS_OFFSET=+')+11,20)+0L
  SCF_SIZE        = STRMID(SCF_DSD, strpos(SCF_DSD, 'DS_SIZE=+')  +9,20)+0L
  SCF_DSR_NUMBER  = STRMID(SCF_DSD, strpos(SCF_DSD, 'NUM_DSR=+')  +9,20)+0L
  SCF_DSR_SIZE    = STRMID(SCF_DSD, strpos(SCF_DSD, 'DSR_SIZE=+') +10,20)+0L

;---------------------------------------
; DEFINE VARIABLE TO HOLD THE SCALING FACTORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: DEFINING DATA ARRAYS FOR OUTPUT'
  SCL_FAC= FLTARR(NB_BANDS)
  NODATA = BYTARR(28)
  
;---------------------------------------------
; POINT TO THE SCALING FACTORS AND READ THE DATA

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: EXTRACTING DATA'
  POINT_LUN,PRD_SF, SCF_OFFSET
  READU,PRD_SF,NODATA
  READU,PRD_SF,SCL_FAC
  
;----------------------------------------
; SWAP ENDIAN IF NEEDED - MERIS DATA IS BIG ENDIAN
  
  IF ENDIAN_SIZE EQ 0 THEN BEGIN
    SCL_FAC = SWAP_ENDIAN(SCL_FAC)
  ENDIF
  
;----------------------------------------
; CLOSE THE PRODUCT AND RETURN THE LUN

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: CLOSING PRODUCTS AND RELEASING THE LUN'
  CLOSE,PRD_SF
  FREE_LUN,PRD_SF 

;----------------------------------------
;RETURN SCALING FACTOR FOR IN_BAND

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B RADIANCE SCALING FACTOR: RETURNING SCALING FACTOR'
  RETURN, SCL_FAC[IN_BAND]

END
