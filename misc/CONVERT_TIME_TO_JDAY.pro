;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      CONVERT_TIME_TO_JDAY       
;* 
;* PURPOSE:
;*      RETURNS EITHER THE IDL JULIAN CALENDER DAY, THE NUMBER OF DAYS SINCE 2000, 
;*      OR THE NUMBER OF DAYS SINCE THE ENVISAT LAUNCH.
;* 
;* CALLING SEQUENCE:
;*      RES = CONVERT_TIME_TO_JDAY(IDATE)      
;* 
;* INPUTS:
;*      IDATE - A STRING OF THE DATE AND TIME TO BE CONVERTED IN ENVISAT MPH FORMAT (E.G. '01-MAY-2005 12:56:00')      
;*
;* KEYWORDS:
;*      ELAUNCH - SET TO COMPUTE JULIAN DAY SINCE ENVISAT LAUNCH
;*      Y2000   - SET TO COMPUTE JULIAN DAY SINCE 2000/01/01
;*      VERBOSE - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      JDAY - THE COMPUTED OUTPUT JULIAN DAY
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      14 DEC 2010 - C KENT    - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT   - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                               COMPILATION AND OPERATION
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION CONVERT_TIME_TO_JDAY,IDATE,ELAUNCH=ELAUNCH,Y2000=Y2000,VERBOSE=VERBOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'CONVERT TIME TO JDAY: STARTING CONVERSION'

;--------------------------------
;DEFINE Y2000 AND ELAUNCH DATES
 
  IF (N_ELEMENTS(ELAUNCH)+N_ELEMENTS(Y2000)) GT 1 THEN BEGIN
    PRINT, 'CONVERT TIME TO JDAY: ERROR, BOTH ELAUNCH AND Y2000 KEYWORDS SET'
    RETURN,-1
  ENDIF
  
  OFFSET = 0
  IF N_ELEMENTS(ELAUNCH) GT 0 THEN OFFSET = JULDAY(3,1,2002,0,0,0)
  IF N_ELEMENTS(Y2000) GT 0 THEN OFFSET = JULDAY(1,1,2000,0,0,0)  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'CONVERT TIME TO JDAY: OFFSET COMPUTED AS ',OFFSET

;--------------------------------
;DEFINE MONTHS
  
  MONTHSTR = ['JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG','SEP','OCT','NOV','DEC']

;--------------------------------
;EXTRACT DAY MONTH YEAR
  
  DATE_DAY    = FIX(STRMID(IDATE,0,2)) 
  DATE_MONTH  = STRMID(IDATE,3,3);IS A STRING E.G. 'JAN' 
  DATE_YEAR   = FIX(STRMID(IDATE,7,4))
  DATE_HOUR   = FIX(STRMID(IDATE,12,2))
  DATE_MIN    = FIX(STRMID(IDATE,15,2))
  DATE_SEC    = FIX(STRMID(IDATE,18,2))

;--------------------------------
;FIND INDEX OF MONTH
  
  TEMP = WHERE(MONTHSTR EQ DATE_MONTH)
  IF TEMP[0] EQ -1 OR N_ELEMENTS(TEMP) GT 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'CONVERT TIME TO JDAY: ERROR DURING MONTH STRING ANALYSIS'
    RETURN, -1
  ENDIF
  TEMP = TEMP+1

;--------------------------------
;CONVERT USING JULDAY

  JDAY = JULDAY(TEMP,DATE_DAY,DATE_YEAR,DATE_HOUR,DATE_MIN,DATE_SEC)-OFFSET

;--------------------------------
;RETURN, NEW JULIAN DAY

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'CONVERT TIME TO JDAY: CONVERSION SUCCESSFUL, RETURNING NEW JDAY'
  RETURN,JDAY

END