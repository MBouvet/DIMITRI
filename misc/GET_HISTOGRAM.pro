;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_HISTOGRAM       
;* 
;* PURPOSE:
;*      GENERATES A HISTOGRAM INSIDE THE Z-BUFFER 
;* 
;* CALLING SEQUENCE:
;*      RES = GET_HISTOGRAM(HFILE,HDATA,NBINS)  
;* 
;* INPUTS:
;*      HFILE    - THE FULL OUTPUT FILENAME OF THE JPG 
;*      HDATA    - AN ARRAY OF DATA FOR HISTOGRAM ANALYSIS
;*      NBINS    - THE NUMBER OF REQUESTED BINS FOR THE HISTOGRAM
;*
;* KEYWORDS:
;*      PLOT_XSIZE  - OUTPUT PIXEL SIZE IN THE X DIRECTION (DEFAULT = 700) 
;*      PLOT_YSIZE  - OUTPUT PIXEL SIZE IN THE Y DIRECTION (DEFAULT = 400)
;*      HTITLE      - TITLE OF THE HISTOGRAM PLOT  
;*      HX_TITLE    - X-TITLE OF THE PLOT
;*      HY_TITLE    - Y-TITLE OF THE PLOT
;*      VERBOSE     - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STATUS      - 1 = NOMINAL, (-1) OR 0 = ERROR
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      19 JAN 2011 - C KENT   - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      13 DEC 2010 - C KENT   -  
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_HISTOGRAM,HFILE,HDATA,NBINS,HTITLE=HTITLE,HX_TITLE=HX_TITLE,HY_TITLE=HY_TITLE,NO_ZBUFF=NO_ZBUFF, $
                       PLOT_XSIZE=PLOT_XSIZE,PLOT_YSOZE=PLOT_YSOZE,VERBOSE=VERBOSE

;------------------------------------------------
; SET KEYWORD VALUES

  IF NOT KEYWORD_SET(PLOT_XSIZE)  THEN PLOT_XSIZE = 700
  IF NOT KEYWORD_SET(PLOT_YSIZE)  THEN PLOT_YSIZE = 400
  IF NOT KEYWORD_SET(HTITLE)      THEN HTITLE     = ''
  IF NOT KEYWORD_SET(HX_TITLE)    THEN HX_TITLE   = ''
  IF NOT KEYWORD_SET(HY_TITLE)    THEN HY_TITLE   = ''

;------------------------------------------------
; SETUP WINDOW PROPERTIES

  MACHINE_WINDOW = !D.NAME
  IF NOT KEYWORD_SET(NO_ZBUFF) THEN BEGIN
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'GET HISTOGRAM: GENERATING PLOTS WITHIN Z-BUFFER'
  SET_PLOT, 'Z'
  DEVICE, SET_RESOLUTION=[PLOT_XSIZE,PLOT_YSIZE],SET_PIXEL_DEPTH=24
  ERASE  
  ENDIF ELSE WINDOW,XSIZE = PLOT_XSIZE,YSIZE=PLOT_YSIZE
  DEVICE, DECOMPOSED = 0

;----------------------------------------------- 
; PLOT THE HISTOGRAM 

  HIST = HISTOGRAM(HDATA,LOCATIONS=BIN, NBINS=NBINS)
  PLOT, BIN, 1.0*HIST/N_ELEMENTS(HDATA), BACKGROUND=255, COLOR=0, $
        CHARSIZE=1.0, XTITLE=HX_TITLE, $
        YTITLE=HY_TITLE,TITLE=HTITLE

  TEMP = TVRD(/TRUE)
  WRITE_JPEG,HFILE,TEMP,TRUE=1

;-----------------------------------------------
; RETURN DEVICE WINDOW TO NOMINAL SETTING
 
  IF KEYWORD_SET(VERBOSE)  THEN PRINT, 'GET HISTOGRAM: RESETTING DEVICE WINDOW PROPERTIES'
  IF KEYWORD_SET(NO_ZBUFF) THEN WDELETE
  SET_PLOT, MACHINE_WINDOW
  RETURN,1

END