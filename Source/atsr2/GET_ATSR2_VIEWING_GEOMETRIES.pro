;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_ATSR2_VIEWING_GEOMETRIES       
;* 
;* PURPOSE:
;*      RETURNS THE VIEWING GEOMETRIES OF A ATSR2 IMAGE
;* 
;* CALLING SEQUENCE:
;*      RES = GET_ATSR2_VIEWING_GEOMETRIES(FILENAME,GEO_DIR)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR EXTRACTION
;*      GEO_DIR  - A STRING CONTAINING THE DIRECTION TO BE RETIREVED, MUST BE EITHER 'NADIR' OR 'FWARD'      
;*
;* KEYWORDS:
;*     VERBOSE      - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*     STRUCT.SZA   - SOLAR ZENITH ANGLE IN DEGREES
;*     STRUCT.SAA   - SOLAR AZIMUTH ANGLE IN DEGREES
;*     STRUCT.VZA   - SENSOR ZENITH ANGLE IN DEGREES
;*     STRUCT.VAA   - SENSOR AZIMUTH ANGLE IN DEGREES
;*
;* COMMON BLOCKS:
;*     NONE 
;*
;* MODIFICATION HISTORY:
;*     10 DEC 2010 - C KENT   - DIMITRI-2 V1.0
;*     12 JUL 2011 - C KENT   - FIXED ANGLE EXTRACTION BUG
;*
;* VALIDATION HISTORY:
;*     13 DEC 2010 - C KENT   - WINDOWS 32-BIT MACHINE idl 7.1: COMPILATION SUCCESSFUL,
;*                              VIEWING GEOMETRY RETIREVAL NOMINAL, IT APPEARS BEAM PROVIDES ELEVATION ANGLE (90 IS NADIR), 
;*                              INSTEAD OF ZENITH ANGLE (0 IS NADIR). VALUES ARE EQUAL TO 90-BEAM VALUE
;*     06 JAN 2010 - C KENT   - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                              VALUES EQUAL TO WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_ATSR2_VIEWING_GEOMETRIES,FILENAME,GEO_DIR,LENGTH,WIDTH,VERBOSE=VERBOSE

;------------------------------------------------
; CHECK FILENAME IS NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'ATSR2 L1B VIEWING GEOMETRIES: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF

;-------------------------------------------------
; OPEN THE PRODUCT AND RETRIEVE HEADER INFORMATION
  
  OPENR, GEO_LUN, FILENAME, /GET_LUN, /SWAP_IF_LITTLE_ENDIAN
  AATSR_GET_HEADERS, GEO_LUN, GEO_MPH, GEO_SPH, GEO_DSD

;------------------------------------------------
; RETRIEVE ADS DATA DEPENDING ON DIRECTION

  CASE GEO_DIR OF
    'NADIR': TEMP = AATSR_READ_ADS_SA(GEO_LUN,GEO_DSD[3],GEO_SPH,0,LENGTH,0,WIDTH)
    'FWARD': TEMP = AATSR_READ_ADS_SA(GEO_LUN,GEO_DSD[4],GEO_SPH,0,LENGTH,0,WIDTH)
  ELSE : BEGIN
    PRINT,  'ATSR2 L1B VIEWING GEOMETRIES: ERRONEOUS DIRECTION, RETURNING'
    RETURN,{ERROR:-1}
         END
  ENDCASE
  
;------------------------------------------------
; CLOSE THE PRODUCT AND FREE THE LUN

  CLOSE,GEO_LUN
  FREE_LUN, GEO_LUN

;------------------------------------------------
; RETURN VIEWING GEOMETRY INFORMATION

  RETURN,{SZA:TEMP.SOLAR.ZENITH,SAA:TEMP.SOLAR.AZIMUTH,VZA:TEMP.SATELLITE.ZENITH,VAA:TEMP.SATELLITE.AZIMUTH}

END


