;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_MERIS_VIEWING_GEOMETRIES       
;* 
;* PURPOSE:
;*      RETURNS THE VIEWING GEOMETRIES OF A MERIS IMAGE
;* 
;* CALLING SEQUENCE:
;*      RES = GET_MERIS_VIEWING_GEOMETRIES(FILENAME)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR EXTRACTION      
;*
;* KEYWORDS:
;*     ENDIAN_SIZE  - MACHINE ENDIAN SIZE (0: LITTLE, 1: BIG)
;*     VERBOSE      - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*     STRUCT.SZA   - SOLAR ZENITH ANGLE IN DEGREES
;*     STRUCT.SAA   - SOLAR AZIMUTH ANGLE IN DEGREES
;*     STRUCT.VZA   - SENSOR ZENITH ANGLE IN DEGREES
;*     STRUCT.VAA   - SENSOR AZIMUTH ANGLE IN DEGREES
;*
;* COMMON BLOCKS:
;*     NONE 
;*
;* MODIFICATION HISTORY:
;*     17 NOV 2005 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*     18 NOV 2010 - C KENT   - DIMITRI-2 V1.0
;*     20 NOV 2010 - C KENT   - UPDATED TO ALLOW SINGULAR USAGE (REMOVED COMMON BLOCKS)
;*     22 NOV 2010 - C KENT   - ADDED VERBOSE KEYWORD OPTION
;*     01 DEC 2010 - C KENT   - ADDED FILENAME ERROR HANDLING
;*     02 DEC 2010 - C KENT   - UPDATED HEADER INFORMATION
;*     23 DEC 2010 - C KENT   - CHANGED CONVERSION TO DOUBLE ARRAYS DUE TO DIFFERENCES 
;*                              BETWEEN WINDOWS/LINUX
;*
;* VALIDATION HISTORY:
;*     01 DEC 2010 - C KENT   - WINDOWS 32-BIT MACHINE: COMPILATION SUCCESSFUL,
;*                              VIEWING GEOMETRY RETIREVAL NOMINAL, VERY SLIGHT 
;*                              DIFFERENCES (~0.0004 DEGREES) AGAINST BEAM VISAT.
;*     05 JAN 2010 - C KENT   - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                              NO DIFFERENCES OBSERVED AGAINST WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_MERIS_VIEWING_GEOMETRIES,FILENAME,ENDIAN_SIZE=ENDIAN_SIZE,VERBOSE=VERBOSE

;------------------------------------------------
; CHECK FILENAME IS NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'MERIS L1B VIEWING GEOMETRIES: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF

;------------------------------------------------
; IF ENDIAN SIZE NOT PROVIDED THEN GET VALUE

  IF N_ELEMENTS(ENDIAN_SIZE) EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN BEGIN
      PRINT, 'MERIS L1B VIEWING GEOMETRIES: NO ENDIAN SIZE PROVIDED, RETRIEVING...'
      ENDIAN_SIZE = GET_ENDIAN_SIZE(/VERBOSE)
    ENDIF ELSE ENDIAN_SIZE = GET_ENDIAN_SIZE()
  ENDIF

;------------------------------------------------
;DEFINE HEADER VARIABLES

  MPH_SIZE = 1247
  SPH_SIZE = 9942
  FILE_MPH = BYTARR(MPH_SIZE)
  FILE_SPH = BYTARR(SPH_SIZE)

;-----------------------------------------------
; OPEN THE FILE AND EXTRACT HEADER

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: OPENING PRODUCT'
  OPENR,PRD_VIG,FILENAME,/GET_LUN
  READU,PRD_VIG,FILE_MPH
  READU,PRD_VIG,FILE_SPH

;-----------------------------------------------
; RETRIEVE: POSITION OF DSD, TP SUBSAMPLING FREQUENCY,LINE SIZE,TP PER LINE,DSD,OFFSET,DS_SIZE,NUMBER OF RECORDS, RECORD SIZE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: RETRIEVING DSD INFORMATION'
  TP_DSD_POS      = STRPOS(FILE_SPH,'DS_NAME="Tie points ADS              "')
  TP_SFREQ        = STRMID(FILE_SPH, STRPOS(FILE_SPH,'SAMPLES_PER_TIE_PT=+')+20, 3)+0L
  LINE_SIZE       = STRMID(FILE_SPH, STRPOS(FILE_SPH,'LINE_LENGTH=+')+13, 5)+0L
  NB_TP_LINE      = FLOOR(LINE_SIZE/TP_SFREQ)+1
  
  TP_DSD          = STRMID(FILE_SPH, TP_DSD_POS,280)
  TP_OFFSET       = STRMID(TP_DSD, STRPOS(TP_DSD, 'DS_OFFSET=+')+11,20)+0L
  TP_SIZE         = STRMID(TP_DSD, STRPOS(TP_DSD, 'DS_SIZE=+')  +9,20)+0L
  TP_DSR_NUMBER   = STRMID(TP_DSD, STRPOS(TP_DSD, 'NUM_DSR=+')  +9,20)+0L
  TP_DSR_SIZE     = STRMID(TP_DSD, STRPOS(TP_DSD, 'DSR_SIZE=+') +10,20)+0L

;-----------------------------------------
; DEFINE ARRAY AND TEMPORARY TO HOLD DATA (SZA,SAA,VZA,VAA)

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: DEFINING DATA ARRAYS FOR OUTPUT'
  VIEW_GEO_SZA = LONARR(NB_TP_LINE,TP_DSR_NUMBER)
  VIEW_GEO_SAA = LONARR(NB_TP_LINE,TP_DSR_NUMBER)
  VIEW_GEO_VZA = LONARR(NB_TP_LINE,TP_DSR_NUMBER)
  VIEW_GEO_VAA = LONARR(NB_TP_LINE,TP_DSR_NUMBER)
  VIEW_REC = LONARR(NB_TP_LINE)
   
;-----------------------------------------
; LOOP OVER EACH RECORD AND EXTRACT ALL VIEWING GEOMETRY DATA  
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: STARTING LOOP FOR DATA EXTRACTION'
  FOR VREC=0,TP_DSR_NUMBER-1 DO BEGIN
 
    POINT_LUN,PRD_VIG, TP_OFFSET+VREC*TP_DSR_SIZE+13+6*4*NB_TP_LINE
    READU,PRD_VIG,VIEW_REC
    VIEW_GEO_SZA[*,VREC] = VIEW_REC

    READU,PRD_VIG,VIEW_REC
    VIEW_GEO_SAA[*,VREC] = VIEW_REC

    READU,PRD_VIG,VIEW_REC
    VIEW_GEO_VZA[*,VREC] = VIEW_REC

    READU,PRD_VIG,VIEW_REC
    VIEW_GEO_VAA[*,VREC] = VIEW_REC
     
  ENDFOR ;END OF LOOP ON VIEWING GEOMETRY RECORDS

;----------------------------------------
; SWAP ENDIAN IF NEEDED - MERIS DATA IS BIG ENDIAN
  
    IF ENDIAN_SIZE EQ 0 THEN BEGIN
      VIEW_GEO_SZA   = SWAP_ENDIAN(VIEW_GEO_SZA)
      VIEW_GEO_SAA   = SWAP_ENDIAN(VIEW_GEO_SAA)
      VIEW_GEO_VZA   = SWAP_ENDIAN(VIEW_GEO_VZA)
      VIEW_GEO_VAA   = SWAP_ENDIAN(VIEW_GEO_VAA)
    ENDIF
  
;---------------------------------------
; CLOSE THE FILE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: CLOSING PRODUCTS AND RELEASING THE LUN'
  CLOSE, PRD_VIG
  FREE_LUN, PRD_VIG
  
;---------------------------------------
; FIND NUMBER OF PIXELS WITHIN MERIS RADIANCE FRAME

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIESN: RETRIEVE DIMENSIONS OF RADIANCE PRODUCT'
  RD_POS      = STRPOS(FILE_SPH,'DS_NAME="Radiance MDS(1)             "')
  RD_DSD      = STRMID(FILE_SPH, RD_POS,280)
  NB_RD_LINE  = STRMID(RD_DSD, STRPOS(RD_DSD, 'NUM_DSR=+')+9,20)  

;---------------------------------------
; CONVERT VALUES INTO CORRECT UNITS
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: CONVERT VALUES INTO DEGREES'
  VIEW_GEO_SZA   = DOUBLE(VIEW_GEO_SZA)*DOUBLE(1e-6)
  VIEW_GEO_SAA   = DOUBLE(VIEW_GEO_SAA)*DOUBLE(1e-6)
  VIEW_GEO_VZA   = DOUBLE(VIEW_GEO_VZA)*DOUBLE(1e-6)
  VIEW_GEO_VAA   = DOUBLE(VIEW_GEO_VAA)*DOUBLE(1e-6)
  
;---------------------------------------
; INTERPOLATE VALUES TO RADIANCE GRID

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: REGRID DATA INTO RADIANCE PRODUCT DIMENSIONS'
  VIEW_GEO_SZA   = CONGRID(VIEW_GEO_SZA,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  VIEW_GEO_SAA   = CONGRID(VIEW_GEO_SAA,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  VIEW_GEO_VZA   = CONGRID(VIEW_GEO_VZA,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  VIEW_GEO_VAA   = CONGRID(VIEW_GEO_VAA,LINE_SIZE,NB_RD_LINE,/MINUS_ONE,/INTERP)
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B VIEWING GEOMETRIES: RETURNING SZA,SAA,VZA,VAA'
  RETURN,{SZA:VIEW_GEO_SZA,SAA:VIEW_GEO_SAA,VZA:VIEW_GEO_VZA,VAA:VIEW_GEO_VAA}
  
END
