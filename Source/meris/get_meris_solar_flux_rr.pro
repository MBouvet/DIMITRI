;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_MERIS_SOLAR_FLUX_RR       
;* 
;* PURPOSE:
;*      RETURNS THE SOLAR SPECTRAL FLUX FOR EACH MERIS DETECTOR
;* 
;* CALLING SEQUENCE:
;*      RES = GET_MERIS_SOLAR_FLUX_RR()      
;* 
;* INPUTS:
;*      NONE        
;*
;* KEYWORDS:
;*      FR      - RETRIEVE SPECTRAL FLUX FOR FULL RESOLUTION DETECTORS (DEFUALT IS RR)          
;*      VERBOSE - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      MIM_RR  - A FLOAT ARRAY CONTAINING THE SPECTRAL FLUX FOR EACH DETECTOR AND BAND 
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      30 JUL 2006 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      17 NOV 2010 - C KENT   - DIMITRI-2 V1.0
;*      22 NOV 2010 - C KENT   - ADDED VERBOSE KEYWORD OPTION
;*      01 DEC 2010 - C KENT   - ADDED AUX DATA FILE ERROR HANDLING
;*      02 DEC 2010 - C KENT   - UPDATED HEADER INFORMATION
;*      21 MAR 2011 - C KENT   - MODIFIED FILE DEFINITION TO USE GET_DIMITRI_LOCATION
;*
;* VALIDATION HISTORY:
;*      01 DEC 2010 - C KENT    - WINDOWS 32-BIT MACHINE: COMPILATION SUCCESSFUL, 
;*                                VALIDATED AGAINST TEXTPAD (FR)/ MANUAL AVERAGING (RR)
;*      05 JAN 2010 - C KENT    - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                                NO DIFFERENCES OBSERVED AGAINST WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_MERIS_SOLAR_FLUX_RR,FR=FR,VERBOSE=VERBOSE

  IF KEYWORD_SET(FR) AND KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: FULL RESOLUTION SELECTED'
  IF NOT KEYWORD_SET(FR) AND KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: REDCUED RESOLUTION SELECTED'

;----------------------------------------
; DEFINE AUXILIARY DATA FILE

  ;MIM2004 = './AUX_DATA/MERIS_Irradiances_Model2004.txt'
  MIM2004 = GET_DIMITRI_LOCATION('MERIS_IM2004')
  
  TEMP = FILE_INFO(MIM2004)
  IF TEMP.EXISTS EQ 0 THEN BEGIN
  PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: ERROR, AUXILIARY DATA NOT FOUND'
  RETURN,-1
  ENDIF

;----------------------------------------
; DEFINE DATA ARRAYS

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: DEFINING DATA ARRAYS'
  FR_SIZE = 3700
  RR_SIZE = 925
  MIM_HDR = STRING(1)
  MIM_FR  = fltarr(16, FR_SIZE)
  MIM_RR  = fltarr(16, RR_SIZE)
  LINE_COUNT = 0
  TEMP    = FLTARR(16)

;----------------------------------------
; OPEN THE IRRADIANCE FILE AND READ THE HEADER

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: OPEN AUX DATA AND READ HEADER'
  OPENR,PRD_MIM,MIM2004,/GET_LUN
  READF,PRD_MIM,MIM_HDR

;----------------------------------------
; READ EACH LINE OF THE FILE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: EXTRACTING DATA FOR EACH LINE OF AUX DATA'
  WHILE NOT EOF(PRD_MIM)DO BEGIN
    
    READF,PRD_MIM,TEMP
    MIM_FR[*,LINE_COUNT]=TEMP
    LINE_COUNT++
  
  ENDWHILE

;---------------------------------------
; CLOSE THE FILE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: CLOSING THE FILE AND RETURING THE LUN'
  CLOSE, PRD_MIM
  FREE_LUN, PRD_MIM

  IF KEYWORD_SET(FR) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: RETURNING FR SPECTRAL FLUX'
    RETURN,MIM_FR
  ENDIF
  
;---------------------------------------
; CONVERT TO REDUCED RESOLUTION

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: CONVERTING TO REDCUED RESOLUTION'
  FOR MIM_IDX=0, RR_SIZE-1 do begin
    MIM_RR[0,MIM_IDX] = MIM_IDX
    FOR MIM_BND=1,15 DO BEGIN
          MIM_RR[MIM_BND,MIM_IDX]=MEAN(MIM_FR[MIM_BND, 4*MIM_IDX:4*(MIM_IDX+1)-1])
    ENDFOR
  ENDFOR
 
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MERIS L1B SOLAR SPECTRUL FLUX: RETURNING RR SPECTRAL FLUX' 
  RETURN,MIM_RR 

END
