;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_VEGETATION_LAT_LON_PLATE_CARREE      
;* 
;* PURPOSE:
;*      COMPUTES THE PER PIXEL VGT-2 GEOLOCATION
;* 
;* CALLING SEQUENCE:
;*      RES = GET_VEGETATION_LAT_LON_PLATE_CARREE(NORTH,SOUTH,EAST,WEST,NUM_PIX_X,NUM_PIX_Y)      
;* 
;* INPUTS:
;*      NORTH       - THE NORTHERN MOST LATITUDE
;*      SOUTH       - THE SOUTHERN MOST LATITUDE
;*      EAST        - THE EASTERN MOST LONGITUDE
;*      WEST        - THE WESTERN MOST LONGITUDE
;*      NUM_PIX_X   - THE NUMBER OF IMAGE PIXELS IN X DIMENSION
;*      NUM_PIX_Y   - THE NUMBER OF IMAGE PIXELS IN Y DIMENSION
;*
;* KEYWORDS:
;*      VERBOSE    - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STRUCT.LAT  - DERIVED PRODUCT LATITUDE
;*      STRUCT.LON  - DERIVED PRODUCT LONGITUDE
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*        18 JUN 2012 - C KENT    - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_VEGETATION_LAT_LON_PLATE_CARREE,NORTH,SOUTH,EAST,WEST,NUM_PIX_X,NUM_PIX_Y,VERBOSE=VERBOSE

;-------------------------------------------
; CALCULATE LAT GRADIENTS AND INTERPOLATE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION LAT LON: DEFINE LAT AND LON GRIDS'
  NEW_GRID_LAT = MAKE_ARRAY(NUM_PIX_X,NUM_PIX_Y,/FLOAT)
  NEW_GRID_LON = MAKE_ARRAY(NUM_PIX_X,NUM_PIX_Y,/FLOAT)

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION LAT LON: INTERPOLATE BETWEEN MAX AND MIN LATITUDES'
  GRAD = (SOUTH - NORTH)/NUM_PIX_Y
  FOR I=0l,NUM_PIX_Y-1 DO BEGIN
    NEW_GRID_LAT[*,I] = NORTH+GRAD*I
  ENDFOR

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION LAT LON: INTERPOLATE BETWEEN MAX AND MIN LONGITUDES'
  GRAD = (EAST - WEST)/NUM_PIX_X
  FOR I=0l,NUM_PIX_X-1 DO BEGIN
    NEW_GRID_LON[I,*] = WEST+GRAD*I
  ENDFOR

;-------------------------------------------
; RETURN NEW LAT AND LON VARIABLES

  RETURN,{LAT:NEW_GRID_LAT,LON:NEW_GRID_LON}

END