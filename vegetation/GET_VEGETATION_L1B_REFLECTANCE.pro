;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_VEGETATION_L1B_REFLECTANCE      
;* 
;* PURPOSE:
;*      RETIREVES THE L1B REFLECTANCE FROM A VEGETATION LOG FILE
;* 
;* CALLING SEQUENCE:
;*      RES = GET_VEGETATION_L1B_REFLECTANCE(LOG_FILE)      
;* 
;* INPUTS:
;*      LOG_FILE   -  THE FULL PATH OF THE PRODUCTS LOG FILE     
;*
;* KEYWORDS:
;*      VERBOSE    - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      TOA_REF    - AN ARRAY CONTAINING THE REFLECTANCE DATA
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*        17 DEC 2010 - C KENT    - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*        02 DEC 2010 - C KENT    - WINDOWS 32BIT MACHINE idl 7.1: COMPILATION AND EXECUTION 
;*                                  SUCCESSFUL. TESTED MULTIPLE OPTIONS ON MULTIPLE 
;*                                  PRODUCTS
;*        06 JAN 2011 - C KENT    - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL, 
;*                                  NO APPARENT DIFFERENCES WHEN COMPARED TO WINDOWS MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_VEGETATION_L1B_REFLECTANCE,LOG_FILE,VERBOSE=VERBOSE

;------------------------------------------------
;CHECK FILE EXISTS

  IF STRCMP(STRING(LOG_FILE),'') THEN BEGIN
    PRINT, 'VEGETATION L1B REFLECTANCE: ERROR, NO INPUT FILES PROVIDED, RETURNING...'
    RETURN,-1
  ENDIF  

;------------------------------------------------
;CONVERT TO REFLECTANCE FILENAMES

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: DEFINING OUTPUT ARRAY'
  NB_BANDS=4
  TEMP = STRLEN(LOG_FILE)
  REF_FILES = STRARR(NB_BANDS)
  REF_FILES[0] = STRING(STRMID(LOG_FILE,0,TEMP-7)+'B0.HDF')
  REF_FILES[1] = STRING(STRMID(LOG_FILE,0,TEMP-7)+'B2.HDF')
  REF_FILES[2] = STRING(STRMID(LOG_FILE,0,TEMP-7)+'B3.HDF')
  REF_FILES[3] = STRING(STRMID(LOG_FILE,0,TEMP-7)+'MIR.HDF')

;------------------------------------------------  
; CONVERSION FACTOR

  REF_SF = 0.0005

;------------------------------------------------
; RETRIEVE B0

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: RETRIEVEING BAND 0'
  HDF_ID = HDF_SD_START(REF_FILES[0],/READ)
  SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'PIXEL_DATA')
  SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
  HDF_SD_GETDATA,SDS_ID,TOA_REF_0
  HDF_SD_ENDACCESS, SDS_ID
  HDF_SD_END,HDF_ID

;------------------------------------------------
; RETRIEVE B2

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: RETRIEVEING BAND 2'
  HDF_ID = HDF_SD_START(REF_FILES[1],/READ)
  SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'PIXEL_DATA')
  SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
  HDF_SD_GETDATA,SDS_ID,TOA_REF_2
  HDF_SD_ENDACCESS, SDS_ID
  HDF_SD_END,HDF_ID

;------------------------------------------------
; RETRIEVE B3

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: RETRIEVEING BAND 3'
  HDF_ID = HDF_SD_START(REF_FILES[2],/READ)
  SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'PIXEL_DATA')
  SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
  HDF_SD_GETDATA,SDS_ID,TOA_REF_3
  HDF_SD_ENDACCESS, SDS_ID
  HDF_SD_END,HDF_ID

;------------------------------------------------
; RETRIEVE BM

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: RETRIEVEING BAND MIR'
  HDF_ID = HDF_SD_START(REF_FILES[3],/READ)
  SDS_NAME = HDF_SD_NAMETOINDEX(HDF_ID, 'PIXEL_DATA')
  SDS_ID=HDF_SD_SELECT(HDF_ID,SDS_NAME)
  HDF_SD_GETDATA,SDS_ID,TOA_REF_M
  HDF_SD_ENDACCESS, SDS_ID
  HDF_SD_END,HDF_ID

;-----------------------------------------------
; DEFINE TOA REF

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VEGETATION L1B REFLECTANCE: APPLYING SCALING FACTOR'
  TOA_DIMS = SIZE(TOA_REF_0)
  TOA_REF = MAKE_ARRAY(TOA_DIMS[1],TOA_DIMS[2],NB_BANDS,/FLOAT)
  
  TOA_REF[*,*,0] = FLOAT(TOA_REF_0)*REF_SF
  TOA_REF[*,*,1] = FLOAT(TOA_REF_2)*REF_SF
  TOA_REF[*,*,2] = FLOAT(TOA_REF_3)*REF_SF
  TOA_REF[*,*,3] = FLOAT(TOA_REF_M)*REF_SF

;------------------------------------------------  
; MAY NEED TO CHECK BAD_FLAG TO REMOVE HIGH REFLECTANCES--------------------------------------------------

;------------------------------------------------   
 ; RETURN GEOLOCAITON INFORMATION

  RETURN,TOA_REF

END