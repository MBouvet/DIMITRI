;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      ROUJEAN_BRDF       
;* 
;* PURPOSE:
;*      COMPUTES THE ROUJEAN BRDF FOR A GIVEN REFERENCE SENSOR AND ANY RECALIBRATED SENSORS
;* 
;* CALLING SEQUENCE:
;*      RES = ROUJEAN_BRDF(OFOLDER,RB_REGION,REF_SENSOR,REF_PROC_VER,BRDF_BIN)     
;* 
;* INPUTS:
;*      OFOLDER       - A STRING OF THE BASE OUTPUT FOLDER
;*      RB_REGION     - A STRING OF THE VALIDATION SITE FOR BRDF COMPUTATION
;*      REF_SENSOR    - A STRING OF THE REFERENCE SENSOR FOR BRDF COMPUTATION
;*      REF_PROC_VER  - A STRING OF THE REFERENCE SENSORS PROCESSING VERSION
;*      BRDF_BIN      - AN INTEGER OF THE BINNING PERIOD REQUIRED (E.G. 5 = 5 DAY BINS)
;*
;* KEYWORDS:    
;*      NO_PLOTS      - SET TO STOP THE ROUTINE GENERTAIN BRDF PLOTS FOR EACH BAND AND EACH BIN
;*      VERBOSE       - PROCESSING STATUS OUTPUTS
;* 
;* OUTPUTS:
;*      STATUS        - 1 = NOMINAL, (-1) OR 0 = ERROR
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      22 MAR 2007 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      24 JAN 2011 - C KENT   - DIMITRI-2 V1.0
;*      25 JAN 2011 - C KENT   - MOVED BRDF PLOTS TO A NEW FUNCTION, 
;*                               ADDED CSV FILE OUTPUT AND NO_PLOTS OPTION
;*      15 APR 2011 - C KENT   - MINOR BUG FIXES
;*
;* VALIDATION HISTORY:
;*      15 APR 2011 - C KENT   - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                               COMPILATION AND OPERATION. TESTED ON MERIS 2ND REPROCESSING 
;*                               WITH MERIS 3RD REPROCESSING AND MODISA COLLECTION 5 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION ROUJEAN_BRDF,OFOLDER,RB_REGION,REF_SENSOR,REF_PROC_VER,BRDF_BIN,NO_PLOTS=NO_PLOTS,VERBOSE=VERBOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STARTING BRDF BINNING AND COMPUTATION"

;-----------------------------------------
; CHECK OFOLDER EXISTS 
  
  RES = FILE_INFO(OFOLDER)
  IF RES.EXISTS EQ 0 THEN BEGIN
    PRINT,"ROUJEAN BRDF: OUTPUT FOLDER DOESN'T EXIST"
    RETURN,-1
  ENDIF
  
;--------------------------------  
; DEFINE I/O FILES

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: DEFINING I/O FILES"
  DL        = GET_DIMITRI_LOCATION('DL')
  RC_FOLDER = STRING(OFOLDER+DL+'RECALIBRATION'+DL)
  RB_FOLDER = STRING(OFOLDER+DL+'ROUJEAN_BRDF' +DL)
  PT_FOLDER = STRING(RB_FOLDER +'BRDF_PLOTS' +DL)
  REF_DATA  = STRING(RC_FOLDER +'RECAL_REF_' +RB_REGION+'_'+    REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  RCB_DATA  = STRING(RC_FOLDER +'RECAL_'     +RB_REGION+'*REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  K1_FILE   = STRING(RB_FOLDER +'ROUJEAN_K1_'+RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  K2_FILE   = STRING(RB_FOLDER +'ROUJEAN_K2_'+RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  K3_FILE   = STRING(RB_FOLDER +'ROUJEAN_K3_'+RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  KERR_FILE = STRING(RB_FOLDER +'ROUJEAN_ER_'+RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.dat')
  CSV_FILE  = STRING(OFOLDER+DL+'ROUJEAN_'   +RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.csv')
  BRDF_LOG  = STRING(RB_FOLDER +'BRDF_LOG_'  +RB_REGION+'_REF_'+REF_SENSOR+'_'+REF_PROC_VER+'.txt')

;--------------------------------
; CREATE ROUJEAN FOLDER IF IT DOESN'T EXIST

  RES = FILE_INFO(RB_FOLDER)
  IF RES.EXISTS NE 1 OR RES.DIRECTORY NE 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: ROUJEAN FOLDER DOESN'T EXIST, CREATING"
    FILE_MKDIR,RB_FOLDER 
  ENDIF

 RES = FILE_INFO(PT_FOLDER)
  IF RES.EXISTS NE 1 OR RES.DIRECTORY NE 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: PLOT FOLDER DOESN'T EXIST, CREATING"
    FILE_MKDIR,PT_FOLDER 
  ENDIF

  RES = FILE_INFO(REF_DATA)
  IF RES.EXISTS NE 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: INPUT FILE DOESN'T EXIST"
    RETURN,0 
  ENDIF
  RESTORE,REF_DATA 
  
  OPENW,OUT_LOG,BRDF_LOG,/GET_LUN
  PRINTF,OUT_LOG,'VALIDATiON SITE               = ',RB_REGION  
  PRINTF,OUT_LOG,'REFERENCE SENSOR              = ',REF_SENSOR
  PRINTF,OUT_LOG,'REFERENCE PROCESSING VERSION  = ',REF_PROC_VER
  PRINTF,OUT_LOG,'BINNING PERIOD                = ',STRTRIM(STRING(BRDF_BIN),2),' DAYS'
  PRINTF,OUT_LOG,'RE-CALIBRATED SENSORS         = '

;--------------------------------
; SEARCH FOR RECALIBRATED DATA 
; FROM OTHER SENSORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: SEARCHING FOR RECALIBRATED DATA"
  RC_FILES = FILE_SEARCH(RCB_DATA)
  IF STRCMP(RC_FILES[0],'') THEN BEGIN
    PRINT,'ROUJEAN BRDF: ERROR, ONLY REF SENSOR DATA FOUND'
    RETURN,0
  ENDIF
  RC_SENSORS = STRARR(N_ELEMENTS(RC_FILES))

;--------------------------------
; GET ARRAY OF ALL RECAL SENSOR NAMES

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: GETTING LIST OF SENSOR NAMES"
  FOR I=0,N_ELEMENTS(RC_FILES)-1 DO BEGIN
    TEMP = STRSPLIT(RC_FILES[I],'_',/EXTRACT)
    TMP = WHERE(STRCMP(TEMP,RB_REGION) EQ 1)
    IF TMP[0] EQ -1 THEN BEGIN
      PRINT,'ROUJEAN BRDF: ERROR, ONLY REF SENSOR DATA FOUND' 
      PRINTF,OUT_LOG,'ERROR, NO OTHER SENSOR DATA FOUND!!!'
      FREE_LUN,OUT_LOG
      RETURN,0
    ENDIF
    RC_SENSORS[I] = TEMP[TMP[N_ELEMENTS(TMP)-1]+1]
    PRINTF,OUT_LOG,'                                ',RC_SENSORS[I]
  ENDFOR

;--------------------------------
; GET NUMBER OF BANDS IN REF SENSOR

  SENSOR_L1B_REF = GD_REF_DATA
  NUM_NO_REFS = 5
  NB_ACQUI_LIMIT = 3
  REF_SIZE = SIZE(SENSOR_L1B_REF)
  NB_BANDS = FIX((REF_SIZE[1]-5)/2.0)

;--------------------------------
; GET EARLIEST AND LATEST DATES AVAILABLE, 
; COMPUTE NUMBER OF DAYS DIVIDED BY BIN_PERIOD

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: DEFINING BIN CHARACTERISTICS"
  RB_START = MIN(SENSOR_L1B_REF[0,*],MAX=RB_END)
  NB_DAYS  = ABS(RB_END-RB_START)/(1.0/365.0)
  NB_ACQUI = CEIL(NB_DAYS/BRDF_BIN)
  
  PRINTF,OUT_LOG,'START BIN DATE                = ',STRTRIM(STRING(RB_START),2)
  PRINTF,OUT_LOG,'END BIN DATE                  = ',STRTRIM(STRING(RB_END)  ,2)
  PRINTF,OUT_LOG,'NUMBER OF DAYS                = ',STRTRIM(STRING(NB_DAYS) ,2)
  PRINTF,OUT_LOG,'NUMBER OF BINS                = ',STRTRIM(STRING(NB_ACQUI),2)
  FREE_LUN,OUT_LOG
  
;--------------------------------
; CREATE K ARRAYS TO HOLD BRDF COEFICIENTS

  NUM_NON_BRDF = 2 ;TIME, NUM ACQUISITIONS
  K1_ROUJEAN  = MAKE_ARRAY(NUM_NON_BRDF+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)
  K2_ROUJEAN  = MAKE_ARRAY(NUM_NON_BRDF+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)
  K3_ROUJEAN  = MAKE_ARRAY(NUM_NON_BRDF+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)
  ERR_ROUJEAN = MAKE_ARRAY(NUM_NON_BRDF+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)
  NMATCHES    = MAKE_ARRAY(/INTEGER,NB_ACQUI,VALUE=0)
  NSENSORS    = MAKE_ARRAY(/INTEGER,NB_ACQUI,VALUE=0)
  BRDF_UCERT_R= MAKE_ARRAY(1+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)
  BRDF_UCERT_S= MAKE_ARRAY(1+NB_BANDS,NB_ACQUI,/FLOAT,VALUE=-999.0)

;--------------------------------
; LOOP OVER EACH BAND IN REF SENSOR
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STARTING LOOP OVER EACH REFERENCE SENSOR BAND"  
  FOR RB_BAND=0,NB_BANDS-1 DO BEGIN
  
    TEMP        = WHERE(SENSOR_L1B_REF[5+RB_BAND,*] GT 0.0)
    IF TEMP[0] EQ -1 THEN GOTO, NEXT_BAND
    BRDF_DATES  = REFORM(SENSOR_L1B_REF[0,TEMP]  ,N_ELEMENTS(TEMP))
    BRDF_VZA    = REFORM(SENSOR_L1B_REF[1,TEMP]  ,N_ELEMENTS(TEMP))
    BRDF_VAA    = REFORM(SENSOR_L1B_REF[2,TEMP]  ,N_ELEMENTS(TEMP))
    BRDF_SZA    = REFORM(SENSOR_L1B_REF[3,TEMP]  ,N_ELEMENTS(TEMP)) 
    BRDF_SAA    = REFORM(SENSOR_L1B_REF[4,TEMP]  ,N_ELEMENTS(TEMP))  
    BRDF_TOAREF = REFORM(SENSOR_L1B_REF[5+RB_BAND,TEMP],N_ELEMENTS(TEMP))
    TEMP_BID    = CONVERT_INDEX_TO_WAVELENGTH(RB_BAND,REF_SENSOR)

;--------------------------------
; LOOP OVER OTHER SENSOR DATA AND 
; CONCATENATE TO REF_DATA

    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STARTING LOOP OVER EACH RECALIBRATED SENSOR" 
    FOR RB_CAL=0,N_ELEMENTS(RC_SENSORS)-1 DO BEGIN
      TEMP_BANDS = SENSOR_BAND_INFO(RC_SENSORS[RB_CAL])

;--------------------------------
; FIND OUT WHICH BAND IT CORRESPONDS TO

      IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STARTING LOOP TO FIND CORRESPONDING BAND DATA"       
      FOR II=0,TEMP_BANDS[0]-1 DO BEGIN
        TEMP_BID2 = CONVERT_INDEX_TO_WAVELENGTH(II,RC_SENSORS[RB_CAL])
        IF STRCMP(TEMP_BID,TEMP_BID2) EQ 1 THEN GOTO, BAND_MATCH
      ENDFOR
      GOTO, NEXT_SENSOR
      BAND_MATCH:

;--------------------------------
; RESTORE THE DATA

      IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: RESTORING RECALIBRATED DATA" 
      RESTORE,RC_FILES[RB_CAL]
      TMP = WHERE(CAL_DATA[NUM_NO_REFS+II,*] GT 0.0)

      IF TMP[0] GT -1 THEN BEGIN
      IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: APPENDING DATA TO GEOMETRIES AND REFLECTANCE ARRAYS"
        BRDF_DATES  = [BRDF_DATES, REFORM(CAL_DATA[0,TMP],  N_ELEMENTS(TMP))]
        BRDF_VZA    = [BRDF_VZA,   REFORM(CAL_DATA[1,TMP],  N_ELEMENTS(TMP))]  
        BRDF_VAA    = [BRDF_VAA,   REFORM(CAL_DATA[2,TMP],  N_ELEMENTS(TMP))] 
        BRDF_SZA    = [BRDF_SZA,   REFORM(CAL_DATA[3,TMP],  N_ELEMENTS(TMP))]  
        BRDF_SAA    = [BRDF_SAA,   REFORM(CAL_DATA[4,TMP],  N_ELEMENTS(TMP))]   
        BRDF_TOAREF = [BRDF_TOAREF,REFORM(CAL_DATA[5+II,TMP],N_ELEMENTS(TMP))]
      ENDIF
    
      CAL_DATA = 0
      NEXT_SENSOR:
    ENDFOR ;LOOP OVER EACH RECALIBRATED SENSOR DATA

;-------------------------------
; SORT OUT RAA ANGLES

      BRDF_RAA  = ABS(BRDF_SAA-BRDF_VAA)
      RES = WHERE(BRDF_RAA GT 180.0,COUNT)
      IF COUNT GT 0 THEN BRDF_RAA[RES] = 360.0 - BRDF_RAA[RES]

;--------------------------------  
; LOOP OVER EACH BINNING PERIOD

    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STARTING LOOP OVER EACH BINNING PERIOD"  
    FOR RB_BIN =0,NB_ACQUI-1 DO BEGIN

      BIN_START = RB_START +(RB_BIN*(BRDF_BIN/365.0))
      BIN_END   = BIN_START+(BRDF_BIN/365.0)
      BIN_LOC = WHERE(BRDF_DATES GE BIN_START AND BRDF_DATES LT BIN_END,COUNT)
     ; IF COUNT LT NB_ACQUI_LIMIT THEN NMATCHES[RB_BIN]=-1 ELSE NMATCHES[RB_BIN]=COUNT
      IF BIN_LOC[0] EQ -1 OR COUNT LT NB_ACQUI_LIMIT THEN GOTO, NEXT_BIN ELSE NMATCHES[RB_BIN]=COUNT

;--------------------------------   
; STORE ACQUISITION DATE OF BIN
    
      TEMP                   = MEAN([BIN_START,BIN_END])
      K1_ROUJEAN[0,RB_BIN]   = TEMP
      K2_ROUJEAN[0,RB_BIN]   = TEMP
      K3_ROUJEAN[0,RB_BIN]   = TEMP
      ERR_ROUJEAN[0,RB_BIN]  = TEMP
      BRDF_UCERT_R[0,RB_BIN] = TEMP
      BRDF_UCERT_S[0,RB_BIN] = TEMP

;--------------------------------  
; COMPUTE BRDF COEFICIENTS  

      IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: COMPUTING ROUJEAN COEFICIENTS"   
      K         = ROUJEAN_BRDF_COEF(BRDF_SZA[BIN_LOC],BRDF_VZA[BIN_LOC],BRDF_RAA[BIN_LOC],BRDF_TOAREF[BIN_LOC],/DEGREES,VERBOSE=VERBOSE)

      K1_ROUJEAN[NUM_NON_BRDF+RB_BAND,RB_BIN] = K[0]
      K2_ROUJEAN[NUM_NON_BRDF+RB_BAND,RB_BIN] = K[1]
      K3_ROUJEAN[NUM_NON_BRDF+RB_BAND,RB_BIN] = K[2]
 
;--------------------------------  
; COMPUTE STDEV OF INVERSION  

      IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: COMPUTING ROUJEAN RHO VALUES FOR BIN"  
      TEMP_RHO    = ROUJEAN_BRDF_COMPUTE_RHO(BRDF_SZA[BIN_LOC],BRDF_VZA[BIN_LOC],BRDF_RAA[BIN_LOC],K,/DEGREES,VERBOSE=VERBOSE)
      RHO_ROUJEAN = MEAN(TEMP_RHO)
      ERR_ROUJEAN[NUM_NON_BRDF+RB_BAND,RB_BIN] = STDDEV((RHO_ROUJEAN-BRDF_TOAREF[BIN_LOC])/BRDF_TOAREF[BIN_LOC])*100.
 
      TEMP_DIFF = BRDF_TOAREF[BIN_LOC]-TEMP_RHO
      RMSE = SQRT(TOTAL(TEMP_DIFF^2)/N_ELEMENTS(TEMP_DIFF))
      BRDF_UCERT_R[1+RB_BAND,RB_BIN] = 3.*RMSE
      BRDF_UCERT_S[1+RB_BAND,RB_BIN] = RMSE/SQRT(N_ELEMENTS(TEMP_DIFF))
 
      NEXT_BIN:
    ENDFOR ;LOOP ON EACH BINNING PERIOD
  NEXT_BAND:
  ENDFOR ;LOOP OVER EACH BAND

;--------------------------------
; FILTER OUT GOOD DATA

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: STORING ONLY THE VALID DATA"    
  RES = WHERE(K1_ROUJEAN[0,*] gt 0.0)
  IF RES[0] GT -1 THEN BEGIN
   K1_ROUJEAN=K1_ROUJEAN[*,RES]
   K2_ROUJEAN=K2_ROUJEAN[*,RES]
   K3_ROUJEAN=K3_ROUJEAN[*,RES]
   ERR_ROUJEAN = ERR_ROUJEAN[*,RES]
   NMATCHES= NMATCHES[RES]
  ENDIF ELSE BEGIN
    PRINT, 'ROUJEAN BRDF: ERROR, NO GOOD DATA FOUND"  
    RETURN,-1
  ENDELSE

;--------------------------------
; ADD NUMBER OF MATCHES 

   K1_ROUJEAN[1,*] =  NMATCHES
   K2_ROUJEAN[1,*] =  NMATCHES
   K3_ROUJEAN[1,*] =  NMATCHES
   ERR_ROUJEAN[1,*] =  NMATCHES

;--------------------------------  
; SAVE THE DATA 

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: SAVING THE ROUJEAN COEFICIENTS" 
  SAVE, K1_ROUJEAN,  FILENAME=K1_FILE
  SAVE, K2_ROUJEAN,  FILENAME=K2_FILE
  SAVE, K3_ROUJEAN,  FILENAME=K3_FILE
  SAVE, ERR_ROUJEAN, FILENAME=KERR_FILE

;-------------------------------- 
; GENERATE CSV FILE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: GENERATING A CSV FILE"  
  DIMS = SIZE(K1_ROUJEAN)
  OPENW,OUTF,CSV_FILE,/GET_LUN
  TYPES = ['ROUJEAN_K1','ROUJEAN_K2','ROUJEAN_K3','ROUJEAN_ERR']
  HEADER = ['BRDF_TYPE','WAVELENGTH',REFORM(STRTRIM(STRING(K1_ROUJEAN[0,*]),2),DIMS[2])]
  PRINTF,OUTF,FORMAT = '(  '+STRTRIM(STRING((1+DIMS[2])),2)+'(A,1H;),1(A))',HEADER
  PRINTF,OUTF,FORMAT = '(  2(A,1H;), '+STRTRIM(STRING((DIMS[2]-1)),2)+'(i,1H;),1(i))','NUM_MATCHES','',K1_ROUJEAN[1,*]
  NB_BANDS = DIMS[1]-NUM_NON_BRDF
  
  FOR OUT_K = 0,3 DO BEGIN
    CASE OUT_K OF
      0 : TEMP = K1_ROUJEAN
      1 : TEMP = K2_ROUJEAN
      2 : TEMP = K3_ROUJEAN
      3 : TEMP = ERR_ROUJEAN
    ENDCASE
    FOR OUT_BAND=0,NB_BANDS-1 DO BEGIN 
     BAND_NAME =  CONVERT_INDEX_TO_WAVELENGTH(OUT_BAND,REF_SENSOR)  
     PRINTF,OUTF, FORMAT=STRING('(  2(A,1H;),'+STRTRIM(STRING((DIMS[2]-1)),2)+'(F15.6,1H;),1(F15.6))'),$
         TYPES[OUT_K],BAND_NAME+'_NM',TEMP[OUT_BAND+NUM_NON_BRDF,*]
    ENDFOR
  ENDFOR
  
  FREE_LUN,OUTF

;-------------------------------- 
; GENERATE BRDF PLOTS

  IF NOT KEYWORD_SET(NO_PLOTS) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,"ROUJEAN BRDF: CALLING BRDF PLOT FUNCTION" 
    RES = ROUJEAN_BRDF_PLOTS(PT_FOLDER,RB_REGION,K1_ROUJEAN,K2_ROUJEAN,K3_ROUJEAN,ERR_ROUJEAN,/VERBOSE)
  ENDIF ELSE RES = 0

;-------------------------------- 
; RETURN STATUS

  IF RES[0] EQ -1 THEN begin
    PRINT, 'ROUJEAN BRDF: ERROR ENCOUNTERED DURING PLOT PROCESSING'
    RETURN,-1
  ENDIF ELSE BEGIN
    PRINT, 'ROUJEAN BRDF: NO ERRORS REPORTED DURING PROCESSING'
    RETURN,1
  ENDELSE

END